plugins {
	id 'org.springframework.boot' version '2.6.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java-library'
	id 'com.palantir.docker' version '0.27.0'
	id "com.palantir.docker-run" version "0.27.0"
	id "com.palantir.docker-compose" version "0.27.0"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

group = 'pho.formulaone.batch'
version = '0.0.1-SNAPSHOT'


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.batch:spring-batch-test'
}

test {
	useJUnitPlatform()
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/docker/dependency")
}

docker {
	tag 'local', 'functional-formula-one-batch:latest'
	tag 'localVersion', "functional-formula-one-batch:${version}"
	name 'functional-formula-one-batch'
	buildArgs([DEPENDENCY: "dependency", APPCLASS: 'pho.formulaone.batch.FunctionalFormulaOneBatchApplication'])
	dockerfile file("Dockerfile")
}

dockerCompose {
	template 'src/main/docker/docker-compose.functionalformulaonebatch.yml'
	dockerComposeFile "$buildDir/docker/ortolanuniverse/docker-compose.yml"
	templateTokens([
			'ffonebatch.version': "$project.version",
	])
}

dockerRun {
	name = "functional-formula-one-batch-main"
	image = "functional-formula-one-batch"
	ports("8080:8080")
}

dockerPrepare.dependsOn unpack

configure(unpack) {
	group = 'docker'
	description = 'Unpacks the boot jar so we can build multi-layer images'
}
